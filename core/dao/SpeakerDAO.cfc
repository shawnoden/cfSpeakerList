<!--- COMPONENT --->
<cfcomponent displayname="SpeakerDAO" output="false" hint="I am the SpeakerDAO class.">

<!--- Pseudo-constructor --->
<cfset variables.instance = {
	datasource = ''
} />
<cffunction name="init" access="public" output="false" returntype="any" hint="I am the constructor method of the SpeakerDAO class.">
  <cfargument name="datasource" type="any" required="true" hint="I am the Datasource bean." />
  <!--- Set the initial values of the Bean --->
  <cfscript>
	variables.instance.datasource = arguments.datasource;
  </cfscript>
  <cfreturn this>
</cffunction>
<!--- PUBLIC METHODS --->
<!--- CREATE --->
<cffunction name="createNewSpeaker" access="public" output="false" returntype="numeric" hint="I insert a new speaker record into the speakers table in the database.">
  <cfargument name="speaker" type="any" required="true" hint="I am the Speaker bean." />
  <cfset var qPutSpeaker = '' />
  <cfset var insertResult = '' />
  <cftry>
  <cfquery name="qPutSpeaker" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#" result="insertResult">
	INSERT INTO speakers
		(
		  speakerKey,
		  userId,
		  firstName,
		  lastName,
		  email,
		  phone,
		  showPhone,
		  twitter,
		  showTwitter,
		  blog,
		  bio,
		  specialties,
		  locations,
		  majorCity,
		  isOnline	 
		) VALUES (
		  <cfqueryparam value="#ARGUMENTS.speaker.getSpeakerKey()#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getUserId()#" cfsqltype="cf_sql_integer" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getFirstName()#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getLastName()#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.speaker.getEmail(), 'repeatable')#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.speaker.getPhone())#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getShowPhone()#" cfsqltype="cf_sql_bit" />,
		  <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.speaker.getTwitter())#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getShowTwitter()#" cfsqltype="cf_sql_bit" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getBlog()#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.speaker.getBio())#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getSpecialties()#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getLocations()#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getMajorCity()#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.speaker.getIsOnline()#" cfsqltype="cf_sql_bit" />
		)
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn 0 />
  </cfcatch>
  </cftry>
   <cfset returnInsertResult = insertResult.GENERATED_KEY />
  <!--- return the id generated by the database --->
<cfreturn returnInsertResult />
</cffunction>

<!--- RETRIEVE - BY ID --->
<cffunction name="getSpeakerByID" access="public" output="false" returntype="any" hint="I return a Speaker bean populated with the details of a specific speaker record.">
  <cfargument name="id" type="numeric" required="true" hint="I am the numeric auto-increment id of the speaker to search for." />
  <cfset var qGetSpeaker = '' />
  <cfset var speakerObject = '' />
  <cftry>
  <cfquery name="qGetSpeaker" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	SELECT speakerId, speakerKey, userId, firstName, lastName, email, phone, showPhone, twitter, showTwitter, blog, bio, specialties, locations, majorCity, isOnline
	, ( SELECT group_concat(programId) FROM speakerPrograms sp WHERE sp.speakerId = s.speakerId) AS speakerPrograms
	FROM speakers s
	WHERE s.speakerId = <cfqueryparam value="#ARGUMENTS.id#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn createObject('component','core.beans.Speaker').init() />
  </cfcatch>
  </cftry>
  <cfif qGetSpeaker.RecordCount>
    <cfreturn createObject('component','core.beans.Speaker').init(
		speakerId  	= qGetSpeaker.speakerId,
		speakerKey	= qGetSpeaker.speakerKey,
		userId     	= qGetSpeaker.userId,
		firstName  	= qGetSpeaker.firstName,
		lastName   	= qGetSpeaker.lastName,
		email      	= APPLICATION.utils.dataDec(qGetSpeaker.email, 'repeatable'),
		phone      	= APPLICATION.utils.dataDec(qGetSpeaker.phone),
		showPhone	= qGetSpeaker.showPhone,
		twitter    	= APPLICATION.utils.dataDec(qGetSpeaker.twitter),
		showTwitter	= qGetSpeaker.showTwitter,
		blog 		= qGetSpeaker.blog,
		bio 		= APPLICATION.utils.dataDec(qGetSpeaker.bio),
		specialties	= qGetSpeaker.specialties,
		locations  	= qGetSpeaker.locations,
		majorCity 	= qGetSpeaker.majorCity,
		isOnline 	= qGetSpeaker.isOnline,
		speakerPrograms = qGetSpeaker.speakerPrograms
    ) />
	<!--- speakerPrograms = qGetSpeaker.speakerPrograms --->
  <cfelse>
    <cfreturn createObject('component','core.beans.Speaker').init() />
  </cfif>
</cffunction>

<!--- RETRIEVE - BY KEY --->
<cffunction name="getSpeakerByKey" access="public" output="false" returntype="any" hint="I return a Speaker bean populated with the details of a specific speaker record.">
  <cfargument name="key" type="string" required="true" hint="I am the string key of the speaker to search for." />
  <cfset var qGetSpeaker = '' />
  <cfset var speakerObject = '' />
  <cftry>
  <cfquery name="qGetSpeaker" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	SELECT speakerId, speakerKey, userId, firstName, lastName, email, phone, showPhone, twitter, showTwitter, blog, bio, specialties, locations, majorCity, isOnline
		, ( SELECT group_concat(programId) FROM speakerPrograms sp WHERE sp.speakerId = s.speakerId) AS speakerPrograms
	FROM speakers s
	WHERE s.speakerKey = <cfqueryparam value="#ARGUMENTS.key#" cfsqltype="cf_sql_varchar" />
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn createObject('component','core.beans.Speaker').init() />
  </cfcatch>
  </cftry>
  <cfif qGetSpeaker.RecordCount>
    <cfreturn createObject('component','core.beans.Speaker').init(
		speakerId  	= qGetSpeaker.speakerId,
		speakerKey	= qGetSpeaker.speakerKey,
		userId     	= qGetSpeaker.userId,
		firstName  	= qGetSpeaker.firstName,
		lastName   	= qGetSpeaker.lastName,
		email      	= APPLICATION.utils.dataDec(qGetSpeaker.email, 'repeatable'),
		phone      	= APPLICATION.utils.dataDec(qGetSpeaker.phone),
		showPhone	= qGetSpeaker.showPhone,
		twitter    	= APPLICATION.utils.dataDec(qGetSpeaker.twitter),
		showTwitter	= qGetSpeaker.showTwitter,
		blog 		= qGetSpeaker.blog,
		bio 		= APPLICATION.utils.dataDec(qGetSpeaker.bio),
		specialties	= qGetSpeaker.specialties,
		locations  	= qGetSpeaker.locations,
		majorCity 	= qGetSpeaker.majorCity,
		isOnline 	= qGetSpeaker.isOnline,
		speakerPrograms = qGetSpeaker.speakerPrograms
    ) />
  <cfelse>
    <cfreturn createObject('component','core.beans.Speaker').init() />
  </cfif>
</cffunction>

<!--- RETRIEVE - BY USER ID --->
<cffunction name="getSpeakerByUserId" access="public" output="false" returntype="any" hint="I return a Speaker bean populated with the details of a specific speaker record.">
  <cfargument name="id" type="numeric" required="true" hint="I am the numeric auto-increment id of the speaker to search for." />
  <cfset var qGetSpeaker = '' />
  <cfset var speakerObject = '' />
  <cftry>
  <cfquery name="qGetSpeaker" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	SELECT speakerId, speakerKey, userId, firstName, lastName, email, phone, showPhone, twitter, showTwitter, blog, bio, specialties, locations, majorCity, isOnline
		, ( SELECT group_concat(programId) FROM speakerPrograms sp WHERE sp.speakerId = s.speakerId) AS speakerPrograms
	FROM speakers s
	WHERE userId = <cfqueryparam value="#ARGUMENTS.id#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn createObject('component','core.beans.Speaker').init() />
  </cfcatch>
  </cftry>
  <cfif qGetSpeaker.RecordCount>
    <cfreturn createObject('component','core.beans.Speaker').init(
		speakerId  	= qGetSpeaker.speakerId,
		speakerKey	= qGetSpeaker.speakerKey,
		userId     	= qGetSpeaker.userId,
		firstName  	= qGetSpeaker.firstName,
		lastName   	= qGetSpeaker.lastName,
		email      	= APPLICATION.utils.dataDec(qGetSpeaker.email, 'repeatable'),
		phone      	= APPLICATION.utils.dataDec(qGetSpeaker.phone),
		showPhone	= qGetSpeaker.showPhone,
		twitter    	= APPLICATION.utils.dataDec(qGetSpeaker.twitter),
		showTwitter	= qGetSpeaker.showTwitter,
		blog 		= qGetSpeaker.blog,
		bio 		= APPLICATION.utils.dataDec(qGetSpeaker.bio),
		specialties	= qGetSpeaker.specialties,
		locations  	= qGetSpeaker.locations,
		majorCity 	= qGetSpeaker.majorCity,
		isOnline 	= qGetSpeaker.isOnline,
		speakerPrograms = qGetSpeaker.speakerPrograms
    ) />
  <cfelse>
    <cfreturn createObject('component','core.beans.Speaker').init() />
  </cfif>
</cffunction>


<!--- SPEAKERPROGRAMS - BY SpeakerID --->
<cffunction name="getSpeakerProgramsBySpeakerId" access="public" output="false" returntype="any" hint="I return a SpeakerPrograms bean populated with the details of a specific speakerPrograms record.">
  <cfargument name="id" type="numeric" required="true" hint="I am the numeric auto-increment id of the speaker to search for." />
  <cfset var qGetSpeakerPrograms = '' />
  <cfset var speakerProgramsObject = '' />
  <cftry>
  <cfquery name="qGetSpeakerPrograms" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
    SELECT sp.speakerId, sp.programID, sp.inactive, p.programName, p.programAbbreviation, p.programOrder, p.programInactive
    FROM speakerPrograms sp
    LEFT OUTER JOIN programs p ON sp.programId = p.programId 
	  AND p.programInactive = 0
    WHERE sp.inactive = 0
	  AND sp.speakerId = <cfqueryparam value="#ARGUMENTS.id#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn createObject('component','core.beans.Speaker').init() />
  </cfcatch>
  </cftry>
  
  <!--- Flatten results. SerializeToJSON() http://cfsimplicity.com/53/simpler-handling-of-json-serialised-coldfusion-query-objects --->
  <cfif qGetSpeaker.RecordCount>
    <cfset speakerProgramsObject = SerializeJSON(qGetSpeakerPrograms) />
  </cfif>
  <cfreturn speakerProgramsObject />
</cffunction>

<!--- UPDATE --->
<cffunction name="updateSpeaker" access="public" output="false" returntype="numeric" hint="I update this speaker record in the speakers table of the database.">
  <cfargument name="speaker" type="any" required="true" hint="I am the Speaker bean." />
  <cfset var qUpdSpeaker = '' />
  <cfset var qUpdSpeakerProgram = '' />
  
  <cftry>
  <cfquery name="qUpdSpeaker" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	UPDATE speakers SET
	  speakerKey = <cfqueryparam value="#ARGUMENTS.speaker.getSpeakerKey()#" cfsqltype="cf_sql_varchar" />,
	  userId = <cfqueryparam value="#ARGUMENTS.speaker.getUserId()#" cfsqltype="cf_sql_int" />,
	  firstName = <cfqueryparam value="#ARGUMENTS.speaker.getFirstName()#" cfsqltype="cf_sql_varchar" />,
	  lastName = <cfqueryparam value="#ARGUMENTS.speaker.getLastName()#" cfsqltype="cf_sql_varchar" />,
	  email = <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.speaker.getEmail(), 'repeatable')#" cfsqltype="cf_sql_varchar" />,
	  phone = <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.speaker.getPhone())#" cfsqltype="cf_sql_varchar" />,
	  showPhone = <cfqueryparam value="#ARGUMENTS.speaker.getShowPhone()#" cfsqltype="cf_sql_bit" />,
	  twitter = <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.speaker.getTwitter())#" cfsqltype="cf_sql_varchar" />,
	  showTwitter = <cfqueryparam value="#ARGUMENTS.speaker.getShowTwitter()#" cfsqltype="cf_sql_bit" />,
	  blog = <cfqueryparam value="#ARGUMENTS.speaker.getBlog()#" cfsqltype="cf_sql_varchar" />,
	  bio = <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.speaker.getBio())#" cfsqltype="cf_sql_varchar" />,
	  specialties = <cfqueryparam value="#ARGUMENTS.speaker.getSpecialties()#" cfsqltype="cf_sql_varchar" />,
	  locations = <cfqueryparam value="#ARGUMENTS.speaker.getLocations()#" cfsqltype="cf_sql_varchar" />,
	  majorCity = <cfqueryparam value="#ARGUMENTS.speaker.getMajorCity()#" cfsqltype="cf_sql_varchar" />,
	  isOnline = <cfqueryparam value="#ARGUMENTS.speaker.getIsOnline()#" cfsqltype="cf_sql_bit" />
	WHERE speakerId = <cfqueryparam value="#ARGUMENTS.speaker.getSpeakerId()#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn 0 />
  </cfcatch>
  </cftry>
  
  <!---
  <cftry>
  <cfquery name="qUpdSpeakerProgram" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	UPDATE speakerPrograms 
	SET inactive = `1`	 
    WHERE speakerId = <cfqueryparam value="#ARGUMENTS.speaker.getSpeakerId()#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn 0 />
  </cfcatch>
  </cftry>
  --->
  <cfreturn ARGUMENTS.speaker.getSpeakerId() />
</cffunction>

<!--- DELETE --->
<cffunction name="deleteSpeakerByID" access="public" output="false" returntype="boolean" hint="I delete a speaker from speaker table in the database.">
  <cfargument name="id" type="numeric" required="true" hint="I am the numeric auto-increment id of the speaker to delete." />
  <cfset var qDelSpeaker = '' />
  <cftry>
    <cfquery name="qDelSpeaker" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
		DELETE FROM speakers
		WHERE speakerId = <cfqueryparam value="#ARGUMENTS.id#" cfsqltype="cf_sql_integer" />
	</cfquery>
    <cfcatch type="database">
      <cfreturn false />
    </cfcatch>
  </cftry>
  <cfreturn true />
</cffunction>

<!--- UTILITY FUNCTIONS --->
<!--- SAVE --->
<cffunction name="saveSpeaker" access="public" output="false" returntype="any" hint="I handle saving a speaker either by creating a new entry or updating an existing one.">
  <cfargument name="speaker" type="any" required="true" hint="I am the Speaker bean." />
  <cfif exists(ARGUMENTS.speaker)>
	<cfreturn updateSpeaker(ARGUMENTS.speaker) />
  <cfelse>
	<cfreturn createNewSpeaker(ARGUMENTS.speaker) />
  </cfif>
</cffunction>

<!--- EXISTS --->
<cffunction name="exists" access="private" output="false" returntype="boolean" hint="I check to see if a specific Speaker is in the database, using ID as the check.">
  <cfargument name="speaker" type="any" required="true" hint="I am the Speaker bean." />
  <cfset var qGetSpeaker = '' />
  <cfquery name="qGetSpeaker" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	SELECT speakerId FROM speakers
	WHERE speakerId = <cfqueryparam value="#ARGUMENTS.speaker.getSpeakerId()#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <cfif qGetSpeaker.RecordCount>
	<cfreturn true />
  <cfelse>
	<cfreturn false />
  </cfif>
</cffunction>
</cfcomponent>

