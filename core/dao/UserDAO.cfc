<!--- COMPONENT --->
<cfcomponent displayname="UserDAO" output="false" hint="I am the UserDAO class.">

<!--- Pseudo-constructor --->
<cfset variables.instance = {
	datasource = ''
} />
<cffunction name="init" access="public" output="false" returntype="any" hint="I am the constructor method of the UserDAO class.">
  <cfargument name="datasource" type="any" required="true" hint="I am the Datasource bean." />
  <!--- Set the initial values of the Bean --->
  <cfscript>
	variables.instance.datasource = arguments.datasource;
  </cfscript>
  <cfreturn this>
</cffunction>
<!--- PUBLIC METHODS --->
<!--- CREATE --->
<cffunction name="createNewUser" access="public" output="false" returntype="numeric" hint="I insert a new user record into the users table in the database.">
  <cfargument name="user" type="any" required="true" hint="I am the User bean." />
  <cfset var qPutUser = '' />
  <cfset var insertResult = '' />
  <cftry>
  <cfquery name="qPutUser" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#" result="insertResult">
	INSERT INTO users
		(
		  username,
		  password,
		  role,
		  isActive
		) VALUES (
		  <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.user.getUsername())#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.user.getPassword())#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.user.getRole())#" cfsqltype="cf_sql_varchar" />,
		  <cfqueryparam value="#ARGUMENTS.user.getIsActive()#" cfsqltype="cf_sql_bit" />
		)
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn 0 />
  </cfcatch>
  </cftry>
  <!--- return the id generated by the database --->
<cfreturn insertResult.GENERATED_KEY />
</cffunction>

<!--- RETRIEVE - BY ID --->
<cffunction name="getUserByID" access="public" output="false" returntype="any" hint="I return a User bean populated with the details of a specific user record.">
  <cfargument name="id" type="numeric" required="true" hint="I am the numeric auto-increment id of the user to search for." />
  <cfset var qGetUser = '' />
  <cfset var userObject = '' />
  <cftry>
  <cfquery name="qGetUser" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	SELECT userId, username, password, role, isActive
	FROM users
	WHERE userId = <cfqueryparam value="#ARGUMENTS.id#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn createObject('component','core.beans.User').init() />
  </cfcatch>
  </cftry>
  <cfif qGetUser.RecordCount>
    <cfreturn createObject('component','core.beans.User').init(
	userId  	= qGetUser.userId,
	username	= APPLICATION.utils.dataDec(qGetUser.username),
	password	= APPLICATION.utils.dataDec(qGetUser.password),
	role    	= APPLICATION.utils.dataDec(qGetUser.role),
	isActive	= qGetUser.isActive
    ) />
  <cfelse>
    <cfreturn createObject('component','core.beans.User').init() />
  </cfif>
</cffunction>

<!--- UPDATE --->
<cffunction name="updateUser" access="public" output="false" returntype="numeric" hint="I update this user record in the users table of the database.">
  <cfargument name="user" type="any" required="true" hint="I am the User bean." />
  <cfset var qUpdUser = '' />
  <cftry>
  <cfquery name="qUpdUser" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	UPDATE users SET
	  username = <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.user.getUsername())#" cfsqltype="cf_sql_varchar" />,
	  password = <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.user.getPassword())#" cfsqltype="cf_sql_varchar" />,
	  role = <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.user.getRole())#" cfsqltype="cf_sql_varchar" />,
	  isActive = <cfqueryparam value="#ARGUMENTS.user.getIsActive()#" cfsqltype="cf_sql_bit" />
	WHERE userId = <cfqueryparam value="#ARGUMENTS.user.getUserId()#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <!--- catch any errors --->
  <cfcatch type="any">
	<cfset APPLICATION.utils.errorHandler(cfcatch) />
	<cfreturn 0 />
  </cfcatch>
  </cftry>
  <cfreturn ARGUMENTS.user.getUserId() />
</cffunction>

<!--- DELETE --->
<cffunction name="deleteUserByID" access="public" output="false" returntype="boolean" hint="I delete a user from user table in the database.">
  <cfargument name="id" type="numeric" required="true" hint="I am the numeric auto-increment id of the user to delete." />
  <cfset var qDelUser = '' />
  <cftry>
    <cfquery name="qDelUser" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
		DELETE FROM users
		WHERE userId = <cfqueryparam value="#ARGUMENTS.id#" cfsqltype="cf_sql_integer" />
	</cfquery>
    <cfcatch type="database">
      <cfreturn false />
    </cfcatch>
  </cftry>
  <cfreturn true />
</cffunction>

<!--- UTILITY FUNCTIONS --->
<!--- SAVE --->
<cffunction name="saveUser" access="public" output="false" returntype="any" hint="I handle saving a user either by creating a new entry or updating an existing one.">
  <cfargument name="user" type="any" required="true" hint="I am the User bean." />
  <cfif exists(ARGUMENTS.user)>
	<cfreturn updateUser(ARGUMENTS.user) />
  <cfelse>
	<cfreturn createNewUser(ARGUMENTS.user) />
  </cfif>
</cffunction>

<!--- EXISTS --->
<cffunction name="exists" access="private" output="false" returntype="boolean" hint="I check to see if a specific User is in the database, using ID as the check.">
  <cfargument name="user" type="any" required="true" hint="I am the User bean." />
  <cfset var qGetUser = '' />
  <cfquery name="qGetUser" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	SELECT userId FROM users
	WHERE userId = <cfqueryparam value="#ARGUMENTS.user.getUserId()#" cfsqltype="cf_sql_integer" />
  </cfquery>
  <cfif qGetUser.RecordCount>
	<cfreturn true />
  <cfelse>
	<cfreturn false />
  </cfif>
</cffunction>

<!---                      --->
<!--- CHECK IF USER EXISTS --->
<!---                      --->
<cffunction name="checkIfUserExists" access="public" returntype="boolean" output="false" hint="I check to see if a specific user is in the database, using their email address as the check.">
  <cfargument name="email" type="string" required="true" hint="I am the email address to check for the existence of." />
  <cfset var qGetUser = '' />
  <cfquery name="qGetUser" datasource="#variables.instance.datasource.getDSN()#" username="#variables.instance.datasource.getUsername()#" password="#variables.instance.datasource.getPassword()#">
	SELECT userId FROM users
	WHERE username = <cfqueryparam value="#APPLICATION.utils.dataEnc(ARGUMENTS.email)#" cfsqltype="cf_sql_varchar" />
  </cfquery>
  <cfif qGetUser.RecordCount>
	<cfreturn true />
  <cfelse>
	<cfreturn false />
  </cfif>
</cffunction>
  
</cfcomponent>

